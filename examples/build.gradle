plugins {
    id 'war'

    // IntelliJ IDEA plugin here to allow integration tests to appear properly in IDEs.
    id 'idea'
}

repositories {
    jcenter()
    mavenLocal()

    // Local repository for CADC private code
    maven {
        url = 'https://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
}

dependencies {
    implementation 'org.opencadc:cadc-vosui:1.1.42'

    testImplementation 'junit:junit:[4.12,5.0)'
    testImplementation 'org.apache.commons:commons-lang3:[3.9,4.0)'
    testImplementation 'org.opencadc:cadc-web-test:[2.1.1,)'
    testImplementation 'org.seleniumhq.selenium:selenium-java:3.14'
}

sourceCompatibility = '1.8'

war {
    archiveName 'storage.war'
}

ext {
    // Change this value to test other locations, such as Production or RC.
    intTest_default_web_app_url = 'http://localhost:8080/storage/list/'
    intTest_user_name = 'CADCtest'
    intTest_user_password = 'sywymUL4'
}

sourceSets {
    intTest
}

configurations {
    intTestCompile
    intTestCompile.extendsFrom(testImplementation)
}

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/intTest/java')
        scopes.TEST.plus += [ configurations.intTestCompile ]
    }
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:4.+'
            intTestCompile 'org.opencadc:cadc-web-test:[2.1.1,)'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:3.+'
        }

        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
            logger.info('Running browser tests on ' + project.intTest_selenium_server_url)
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:4444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }
        else {
            systemProperty 'web.app.url', project.intTest_default_web_app_url
        }

        systemProperty 'user.name', project.intTest_user_name
        systemProperty 'user.password', project.intTest_user_password

        maxParallelForks = 3
    }
}
